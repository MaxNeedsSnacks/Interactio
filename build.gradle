plugins {
    id "com.github.johnrengelman.shadow" version "5.0.0"
    id "forgified-fabric-loom" version "0.6.67"
    id "java"
    id "idea"
}

def ENV = System.getenv()

archivesBaseName = project.archives_base_name
version = "${mod_version}.${ENV.BUILD_NUMBER ?: "custom"}"
group = project.maven_group

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

loom {
    silentMojangMappingsLicense()
    useFabricMixin = true
    mixinConfig "interactio.mixins.json"
}

repositories {
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        url = "https://maven.shedaniel.me"
    }
}

dependencies {
    minecraft("com.mojang:minecraft:${project.minecraft_version}")

    mappings(minecraft.officialMojangMappings())
    //mappings("de.oceanlabs.mcp:mcp_snapshot:20201028-1.16.3")

    forge("net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}")

    modCompile("me.shedaniel:architectury-forge:${architectury_version}")

    modCompile("mezz.jei:jei-1.16.4:${jei_version}")

    modCompile("curse.maven:kubejs-238086:3189113")
    modCompile("curse.maven:rhino-416294:3187177")

    modRuntime("curse.maven:top-245211:3157997")

    // nullable annoations
    compileOnly("com.google.code.findbugs:jsr305:3.+")
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"

    def targetVersion = 8
    if (JavaVersion.current().isJava9Compatible()) {
        options.release = targetVersion
    }
}

tasks.withType(JavaExec) {
	if (System.getProperty('DEBUG', 'false') == 'true') {
		jvmArgs '-Xdebug', 'Xrunjdwp:transport=dt_socket,server=y,suspend=y,adress=9099'
	}
}