plugins {
    id "com.github.johnrengelman.shadow" version "5.0.0"
    id "forgified-fabric-loom" version "0.6-SNAPSHOT"
    id "java"
    id "idea"
}

def ENV = System.getenv()

archivesBaseName = project.archives_base_name
version = "${mod_version}.${ENV.BUILD_NUMBER ?: "custom"}"
group = project.maven_group

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

loom {
    silentMojangMappingsLicense()
    useFabricMixin = true
    mixinConfig "interactio.mixins.json"
}

repositories {
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        url = "https://dvs1.progwml6.com/files/maven/"
        content {
            includeGroup "mezz.jei"
        }
    }
    maven {
        url = "https://maven.architectury.dev"
        content {
            includeGroup "me.shedaniel"
            includeGroup "dev.architectury"
        }
    }
    maven {
        url = "https://maven.saps.dev/minecraft"
        content {
            includeGroup "dev.ftb.mods"
            includeGroup "dev.latvian.mods"
        }
    }
}

dependencies {
    minecraft("com.mojang:minecraft:${project.minecraft_version}")

    mappings(minecraft.officialMojangMappings())

    forge("net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}")

    modApi("me.shedaniel:architectury-forge:${architectury_version}")

    modImplementation("mezz.jei:jei-1.16.4:${jei_version}")
    modImplementation("dev.latvian.mods:kubejs-forge:1605.3.+")

    modRuntime("curse.maven:top-245211:3157997")

    // nullable annoations
    compileOnly("com.google.code.findbugs:jsr305:3.+")
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"

    def targetVersion = 8
    if (JavaVersion.current().isJava9Compatible()) {
        options.release = targetVersion
    }
}